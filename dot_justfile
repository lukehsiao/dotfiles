# just manual: https://github.com/casey/just

whoami := env('USER')

_default:
    @just --list

# Install Iosevka font via GitHub release
_iosevka:
    #!/usr/bin/env bash
    set -euxo pipefail
    curl -s https://api.github.com/repos/be5invis/Iosevka/releases/latest | rg -N "browser_download_url" | rg -N --color never "SuperTTC-Iosevka-\d+\.\d+\.\d+\.zip" | sd '"' "'" | choose 1 | xargs -I % xh -F -o /tmp/iosevka.zip GET %
    unzip -o /tmp/iosevka.zip -d $HOME/.local/share/fonts/
    rm /tmp/iosevka.zip
    fc-cache -f -v

# Install Rust
_rustup:
    #!/usr/bin/env bash
    set -euxo pipefail

    if ! command -v rustup >/dev/null; then
        # Install rustup and Rust stable
        curl --proto '=https' --tlsv1.2 -sSf https://sh.rustup.rs | sh -s -- -y
        source "$HOME/.cargo/env"
        rustup default stable
        rustup update
        echo "Installed Rust"
    else
        echo "Rust is already installed."
    fi

# Install Zulip (AppImage)
_zulip:
    #!/usr/bin/env bash
    set -euxo pipefail

    if ! command -v zulip >/dev/null; then
        # Install zulip
        xh --download https://zulip.com/apps/download/linux --output zulip
        chmod +x zulip
        sudo install zulip /usr/bin/zulip
        rm zulip
        echo "Installed Zulip"
    else
        echo "Zulip is already installed."
    fi

# Install d2
_d2:
    curl -fsSL https://d2lang.com/install.sh | sh -s --

# Install tailscale
_tailscale:
    omarchy-install-tailscale

# Install Rust core tooling
_rust-core: _rustup
    #!/usr/bin/env bash
    set -euxo pipefail

    # ensure cargo is sourced
    if ! command -v rustup >/dev/null; then
        source "$HOME/.cargo/env"
    fi

    # Install binstall first
    curl -L --proto '=https' --tlsv1.2 -sSf https://raw.githubusercontent.com/cargo-bins/cargo-binstall/main/install-from-binstall-release.sh | bash

    cargo binstall -y --locked \
        bunyan \
        cargo-audit \
        cargo-bloat \
        cargo-deny \
        cargo-edit \
        cargo-leptos \
        cargo-nextest \
        cargo-semver-checks \
        cargo-update \
        choose \
        dircnt \
        flamegraph \
        git-stats \
        leptosfmt \
        openring \
        pgen \
        ren-find \
        rep-grep \
        rimage \
        sqlx-cli \
        star-history \
        talk-timer \
        titlecase \
        toml-fmt \
        typeracer \
        wasm-bindgen-cli

    cargo install --git https://github.com/lukehsiao/tool.git --locked

    # Install tooling for leptos
    rustup target add wasm32-unknown-unknown

    # Install rust-analyzer
    rustup component add rust-analyzer

# Install dictd and offline dictionaries
_install-dictd:
    sudo pacman -S dictd
    paru -S dict-gcide wordnet-dictd dict-moby-thesaurus
    sudo systemctl enable --now dictd.service

# Remove unused Omarchy Apps
_cleanup-omarchy:
    # Remove unused webapps
    omarchy-webapp-remove \
        Basecamp \
        Discord \
        HEY \
        WhatsApp \
        X

    # Remove unused packages
    yay -Rns obsidian typora

# Install all core tools
[group('install')]
install-core: 
    yay -Rns tldr || true

    yay -S --noconfirm --needed \
        aerc \
        ardour \
        asciinema \
        atuin \
        audacity \
        b3sum \
        bacon \
        base-devel \
        bash-language-server \
        bat \
        bc \
        bottom \
        btop \
        caligula-bin \
        ccid \
        clang \
        clang-tools-extra \
        cmake \
        codebook-lsp \
        coreutils \
        cppcheck \
        croc \
        curl \
        difftastic \
        dmidecode \
        dog \
        duf \
        dust \
        eza \
        fastfetch \
        fd \
        ffmpeg \
        findutils \
        fish \
        fzf \
        gdb \
        ghostty \
        git \
        git-absorb \
        git-cliff \
        git-delta \
        git-filter-repo \
        git-grab \
        git-lfs \
        git-sizer \
        gping \
        helix \
        helm \
        hexyl \
        htop \
        hyperfine \
        imagemagick \
        jaq \
        jless \
        just \
        k9s \
        kubectl \
        less \
        libusb-compat \
        lychee \
        magic-wormhole \
        marksman \
        mat2 \
        mise \
        mold \
        mosh \
        mpv \
        neovim \
        ninja \
        numbat \
        oha \
        onefetch \
        oxipng \
        pandoc \
        parallel \
        paru-bin \
        pass \
        pastel \
        pcsclite \
        pipewire \
        pipewire-audio \
        pinentry \
        podman \
        postgresql \
        presenterm \
        prettier \
        ripgrep \
        rsync \
        samply \
        sccache \
        sd \
        shellcheck \
        shfmt \
        slack-desktop-wayland \
        slurp \
        speedtest-cli \
        sqlite \
        sshx \
        starship \
        strace \
        svgcleaner \
        tailspin \
        taplo-cli \
        tcpdump \
        tealdeer \
        tectonic \
        tinymist \
        tokei \
        tombi \
        trippy \
        typos \
        typos-lsp \
        typst \
        typstyle \
        units \
        unzip \
        valgrind \
        vim \
        vscode-langservers-extracted \
        watchexec \
        wireshark-qt \
        wget \
        wl-clipboard \
        xh \
        xsv \
        xz \
        yaml-language-server \
        yt-dlp \
        yubikey-manager \
        zathura \
        zathura-pdf-poppler \
        zellij \
        zen-browser-bin \
        zola \
        zoxide \
        zstd 

    # use Zen browser as default
    xdg-settings set default-web-browser zen.desktop

    # add to wireshark group to use wireshark
    sudo usermod -aG wireshark {{whoami}}

    # hx as helix
    sudo ln -sf $(which helix) /usr/bin/hx

    # allow Mosh
    sudo ufw enable
    sudo ufw allow 60000:61000/udp comment "Mosh"

    # enable sshd
    sudo systemctl enable sshd

    just _rust-core
    just _iosevka
    just _d2
    just _zulip
    just _install-dictd
    just _tailscale

    # allow tailscale
    sudo ufw allow in on tailscale0

    # Use fish as default
    chsh -s /usr/bin/fish

    # Install webapps
    omarchy-webapp-install DuckAI https://duck.ai https://cdn.jsdelivr.net/gh/homarr-labs/dashboard-icons/png/duckduckgo.png
    omarchy-webapp-install "Google Voice" https://voice.google.com https://cdn.jsdelivr.net/gh/homarr-labs/dashboard-icons/png/google-voice.png
    omarchy-webapp-install "Google Calendar" https://calendar.google.com https://cdn.jsdelivr.net/gh/homarr-labs/dashboard-icons/png/google-calendar.png

    @echo "Installed all core tools."

    just _cleanup-omarchy

# Import my public GPG key
[group('install')]
import-gpg:
    curl --proto '=https' --tlsv1.2 -sSf https://luke.hsiao.dev/publickey.txt | gpg --import
    @echo "Now, remember to edit the key with trust."


# Set TCP congestion control to BBR
[group('install')]
use-bbr:
    #!/usr/bin/env bash
    set -euo pipefail

    # config files
    MODULES_FILE="/etc/modules-load.d/bbr.conf"
    SYSCTL_FILE="/etc/sysctl.d/99-bbr.conf"

    if [[ ! -f "$MODULES_FILE" ]]; then
        echo "Creating $MODULES_FILE"
        echo "tcp_bbr" | sudo tee "$MODULES_FILE" >/dev/null
    else
        echo "$MODULES_FILE already exists, skipping."
    fi

    if [[ ! -f "$SYSCTL_FILE" ]]; then
        echo "Creating $SYSCTL_FILE"
        sudo tee "$SYSCTL_FILE" >/dev/null <<EOF
    net.core.default_qdisc=cake
    net.ipv4.tcp_congestion_control=bbr
    EOF
    else
        echo "$SYSCTL_FILE already exists, skipping."
    fi

    # Load modules if not already loaded
    if ! lsmod | awk '{print $1}' | grep -qx "tcp_bbr"; then
        echo "Loading tcp_bbr"
        sudo modprobe tcp_bbr
    else
        echo "tcp_bbr module already loaded"
    fi
    sudo sysctl --system >/dev/null

    echo -n "Available CC: "
    sysctl -n net.ipv4.tcp_available_congestion_control
    echo -n "CC: "
    sysctl -n net.ipv4.tcp_congestion_control
    echo -n "qdisc: "
    sysctl -n net.core.default_qdisc
